// DESTROYED VILLAGE
// ARCHITECTURE: NED
// SCRIPTING: NED

// **********************************************************************
// Extended-Gametype Mapscript Version 1.1.2 (11-15-04)
// By Mark Follett (Mefy)
// email: mef123@geocities.com
// web: www.planetmedalofhonor.com/mefy
// You are free to modify and redistribute as long as you keep these
// credits.
// **********************************************************************

main:

// set scoreboard messages
setcvar "g_obj_alliedtext1" "Destroyed Village"
setcvar "g_obj_alliedtext2" ""
setcvar "g_obj_alliedtext3" ""
setcvar "g_obj_axistext1" ""
setcvar "g_obj_axistext2" ""
setcvar "g_obj_axistext3" ""

setcvar "g_scoreboardpic" "mohdm2"

	level.mef_mapname = "mohdm2"
	level.mef_maptype = "dm"
	level.mef_supportedgametypes = "ffa"::"tdm"::"rbm"::"ft"::"ctf"::"ftctf"::"dem"::"ftdem"
	level.mef_defaultgametype = "ft"

	switch (waitthread global/libmef/util.scr::get_gametype)
	{
		case "ctf":
		case "ftctf":
			waitthread setup_bases
			thread global/libmef/ctf.scr::ctf_begin
			break
		
		case "dem":
		case "ftdem":
			waitthread setup_bases
			thread global/libmef/dem.scr::dem_begin
			break

		case "ft":
			thread global/libmef/ft.scr::ft_begin
			break

		case "rbm":
			thread roundbasedthread
			break
	}

	level waittill prespawn

	//*** Precache Dm Stuff
	exec global/DMprecache.scr

	exec global/door_locked.scr::lock
	level.script = maps/dm/mohdm2.scr
	exec global/ambient.scr mohdm2
	
	level waittill spawn

	//add in our kill brush under the cheat point.
	local.killbrush = spawn trigger_hurt
	local.killbrush damage 10000
	local.killbrush.origin = ( -2200 2200 -80 )
	local.killbrush setsize ( -1000 -1000 -120 ) ( 1000 1000 0 )

end

//-----------------------------------------------------------------------------

roundbasedthread:

	// Can specify different scoreboard messages for round based games here.

	level waitTill prespawn

	level waittill spawn

	// set the parameters for this round based match
	level.dmrespawning = 0 // 1 or 0
	level.dmroundlimit = 5 // round time limit in minutes
	level.clockside = kills // set to axis, allies, kills, or draw

	level waittill roundstart

end


setup_bases:
	// common bases for dem and ctf
	waitthread global/libmef/bases.scr::addbasepair "-2162 -2584 -123.02 20 Allied Halftrack" "-1278 1038 13.08 155 88mm Flak Cannon" m3 flak88
	waitthread global/libmef/bases.scr::addbasepair "-754.96 -2688.59 -45.18 60 Sherman Tank" "-2684.66 639.32 37.35 -114 Panzer Tank" shermantank panzer ( 7 60 358 ) ( 0 246 2 )
	waitthread global/libmef/bases.scr::addbasepair "-1428 -3408 -17.32 0 East Bldg Ammo Crate" "-2629 1765 216.13 0 Barn Ammo Crate" indycrate indycrate
	waitthread global/libmef/bases.scr::addbasepair "-3163.42 -1963.39 -68.49 70 GMC Truck" "506.45 -284.35 -57.75 -116 Opel Truck" gmctruck opeltruck ( 0 70 3 ) ( 3 244 358 )
	waitthread global/libmef/bases.scr::addbasepair "-2386 -1936 8.12 -40 Center Bldg HQ" "-569 207 40.12 60 Church HQ" cardtable cardtable

	// additional bases for ctf only
	if (level.mef_gametype == "ctf" || level.mef_gametype == "ftctf")
	{
		// southeast/northwest
		waitthread global/libmef/bases.scr::addbasepair "-3320 -2449 -22.94 60" "296 864 18.11 180"

		// east/west
		waitthread global/libmef/bases.scr::addbasepair "-1895 -1978 0.13 180" "-1036 1320 28.47 -90"

		// northeast/southwest
		waitthread global/libmef/bases.scr::addbasepair "-901 -3440 76.09 90" "-3091 806 24.09 -60"
	}		
end
