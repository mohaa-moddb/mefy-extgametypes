// STALINGRAD
// ARCHITECTURE: ZIED, POWZER
// SCRIPTING: POWZER

// **********************************************************************
// Extended-Gametype Mapscript Version 1.1.3 (11-15-04)
// By Mark Follett (Mefy)
// email: mef123@geocities.com
// web: www.planetmedalofhonor.com/mefy
// You are free to modify and redistribute as long as you keep these
// credits.
// **********************************************************************

main:

// set scoreboard messages
setcvar "g_obj_alliedtext1" "Stalingrad"
setcvar "g_obj_alliedtext2" ""
setcvar "g_obj_alliedtext3" ""
setcvar "g_obj_axistext1" ""
setcvar "g_obj_axistext2" ""
setcvar "g_obj_axistext3" ""

setcvar "g_scoreboardpic" "mohdm6"

	level.mef_mapname = "mohdm6"
	level.mef_maptype = "dm"
	level.mef_supportedgametypes = "ffa"::"tdm"::"rbm"::"ft"::"ctf"::"ftctf"::"dem"::"ftdem"
	level.mef_defaultgametype = "ft"

	switch (waitthread global/libmef/util.scr::get_gametype)
	{
		case "ctf":
		case "ftctf":
			waitthread setup_randomized_ctf_bases
			thread global/libmef/ctf.scr::ctf_begin
			break

		case "ft":
			waitthread global/libmef/spawn.scr::spawnblock_begin
			thread global/libmef/ft.scr::ft_begin
			break

		case "dem":
		case "ftdem":
			waitthread setup_dem_bases
			thread global/libmef/dem.scr::dem_begin
			break

		case "rbm":
			waitthread global/libmef/spawn.scr::spawnblock_begin
			thread roundbasedthread
			break
	}

	level waittill prespawn
	
	//*** Precache Dm Stuff
	exec global/DMprecache.scr

	exec global/door_locked.scr::lock
	level.script = maps/dm/mohdm6.scr
	exec global/ambient.scr mohdm6
	
	level waittill spawn

end

//-----------------------------------------------------------------------------

roundbasedthread:

	// Can specify different scoreboard messages for round based games here.

	level waitTill prespawn

	level waittill spawn

	// set the parameters for this round based match
	level.dmrespawning = 0 // 1 or 0
	level.dmroundlimit = 5 // round time limit in minutes
	level.clockside = kills // set to axis, allies, kills, or draw

	level waittill roundstart

end


setup_dem_bases:
	level.mef_settings["ticktime"] = 60

	if (level.mef_gametype == "dem")
	{
		level.mef_settings["respawn"] = 1
	}

	waitthread global/libmef/util.scr::read_gametype_settings

	if (level.mef_gametype == "ftdem" || !level.mef_settings["respawn"])
	{
		waitthread global/libmef/spawn.scr::spawnblock_begin

		if (level.mef_alliedspawnregion == "se")
		{
			local.seteam = "allies"
			local.nwteam = "axis"
		} else
		{
			local.seteam = "axis"
			local.nwteam = "allies"
		}
	} else
	{
		local.seteam = "both"
		local.nwteam = "both"
	}
	
//      -- Base Locations --
//                                                  Team         Coordinates          Angle Description
//                                                  ======       ==================== ===== ===========================================
	waitthread global/libmef/bases.scr::addbase local.seteam " -898  904  496.13    -90 South Bldg Top Level"
	waitthread global/libmef/bases.scr::addbase local.seteam "-1164 -872  272.13     45 South Bldg Staircase"
	waitthread global/libmef/bases.scr::addbase local.seteam " -959  904  256.13    -90 South Bldg Bottom Level"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  379  447  408.13     90 West Bldg Mid Stairs"
	waitthread global/libmef/bases.scr::addbase local.seteam " -108 -624   32.13     90 East Bldg Bottom Level"
	waitthread global/libmef/bases.scr::addbase local.seteam " -103 -624  272.13     90 East Bldg Top Level"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  -39  840   32.13    -90 West Bldg First Floor South Side"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  752  467   32.13   -180 West Bldg First Floor North Side Balcony"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  -82  865  272.13    -90 West Bldg Second Floor South Side"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  752  468  272.13   -180 West Bldg Second Floor North Side Balcony"
	waitthread global/libmef/bases.scr::addbase local.nwteam " -224  585  512.13      0 West Bldg Third Floor South Side"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  928  557  512.13    180 West Bldg Third Floor North Side"
	waitthread global/libmef/bases.scr::addbase local.nwteam "  339  563 -151.88     90 West Bldg Staircase Bottom"
	waitthread global/libmef/bases.scr::addbase both         "  673  160 -151.88    -90 Boiler Room"
	waitthread global/libmef/bases.scr::addbase both         "  223 -129   32.13      0 Locker Room"
	waitthread global/libmef/bases.scr::addbase both         " 1259 -363  304.13    145 North-East Roof Corner"
	waitthread global/libmef/bases.scr::addbase both         "  176 -240 -115.00    145 Courtyard North Side"
	waitthread global/libmef/bases.scr::addbase both         " -784  148 -145.22      0 Courtyard South Side"
end


setup_randomized_ctf_bases:
	waitthread setup_spawns

	// lists the possible base locations for the map
	level.ctf_spots[0]  = waitthread new_ctf_spot "1132 -368 304.13 90" "NR"  // North Roof
	level.ctf_spots[1]  = waitthread new_ctf_spot "-1164 301 496.13 0"  "S3"  // South Bldg 3rd Floor
	level.ctf_spots[2]  = waitthread new_ctf_spot "-1164 467 256.13 0"  "S2"  // South Bldg 2nd Floor
	level.ctf_spots[3]  = waitthread new_ctf_spot "-553 -872 272.13 90" "ED"  // East Bldg Dresser
	level.ctf_spots[4]  = waitthread new_ctf_spot "-132 -624 272.13 90" "E2"  // East Bldg 2nd Floor
	level.ctf_spots[5]  = waitthread new_ctf_spot "-107 -624 32.12 90"  "E1"  // East Bldg 1st Floor
	level.ctf_spots[6]  = waitthread new_ctf_spot "176 656 512.13 180"  "W3S" // West Bldg 3rd Floor South Side
	level.ctf_spots[7]  = waitthread new_ctf_spot "829 1088 512.13 -90" "W3N" // West Bldg 3rd Floor North Side
	level.ctf_spots[8]  = waitthread new_ctf_spot "-512 699 272.13 0"   "W2S" // West Bldg 2nd Floor South Side
	level.ctf_spots[9]  = waitthread new_ctf_spot "829 1088 272.14 -90" "W2N" // West Bldg 2nd Floor North Side
	level.ctf_spots[10] = waitthread new_ctf_spot "-512 697 32.13 0"    "W1S" // West Bldg 1st Floor South Side
	level.ctf_spots[11] = waitthread new_ctf_spot "829 1088 32.13 -90"  "W1N" // West Bldg 1st Floor North Side
	level.ctf_spots[12] = waitthread new_ctf_spot "646 -186 32.13 90"   "B"   // Boiler Room

	for (local.i = 0; local.i < level.ctf_spots.size; local.i++)
	{
		level.ctf_spotlu[level.ctf_spots[local.i].abbr] = level.ctf_spots[local.i]
	}

	// determines which base locations can be selected along with the given base
	level.ctf_spotcomps["NR"]     = "S3"::"S2"::"ED"::"E2"::"W3S"::"W2S"::"W1S"
	level.ctf_spotcomps["S3"]     = "E2"::"W3N"::"W2S"::"W2N"::"B"
	level.ctf_spotcomps["S2"]     = "E2"::"E1"::"W2S"::"W2N"::"W1S"::"W1N"::"B"
	level.ctf_spotcomps["ED"]     = "W3S"::"W3N"::"W2S"::"W2N"::"W1S"::"W1N"::"B"
	level.ctf_spotcomps["E2"]     = "W3S"::"W3N"::"W2S"::"W2N"::"W1S"::"W1N"
	level.ctf_spotcomps["E1"]     = "W1S"::"W1N"::"B"
	level.ctf_spotcomps["W3S"]    = "W1N"::"B"
	level.ctf_spotcomps["W3N"]    = "W1S"::"W1N"::"B"
	level.ctf_spotcomps["W2S"][1] = "B"
	level.ctf_spotcomps["W2N"][1] = "B"
	level.ctf_spotcomps["W1S"][1] = "B"
	level.ctf_spotcomps["W1N"][1] = "B"

	// determines which spawns are disabled for the given base
	// prevents the opposing team from spawning near a team's base
	level.ctf_spotspawn["NR"]  = "NR"::"W3N"
	level.ctf_spotspawn["S3"]  = "S3"::"SST"
	level.ctf_spotspawn["S2"]  = "S3"::"S2"::"SSB"
	level.ctf_spotspawn["ED"]  = "SST"::"SSB"::"ES2"
	level.ctf_spotspawn["E2"]  = "E2"::"ES2"
	level.ctf_spotspawn["E1"]  = "E1"::"E1B"::"ES1"
	level.ctf_spotspawn["W3S"] = "S3"::"W3SB"::"W3S"::"W3N"
	level.ctf_spotspawn["W3N"] = "W3SB"::"W3S"::"W3N"
	level.ctf_spotspawn["W2S"] = "W2SB"::"W2S"::"W2N"
	level.ctf_spotspawn["W2N"] = "W2SB"::"W2S"::"W2N"
	level.ctf_spotspawn["W1S"] = "W1SB"::"W1S"::"W1N"
	level.ctf_spotspawn["W1N"] = "W1SB"::"W1S"::"W1N"
	level.ctf_spotspawn["B"]   = "B"::"CS"

	local.spotconfigs = waitthread enumerate_spot_configs level.ctf_spots level.ctf_spotcomps
	local.spotconfig = local.spotconfigs[randomint(local.spotconfigs.size)]

	if (randomint(2) == 0)
	{
		local.alliedspot = local.spotconfig[0]
		local.axisspot = local.spotconfig[1]
	} else
	{
		local.alliedspot = local.spotconfig[1]
		local.axisspot = local.spotconfig[0]
	}
	
	waitthread block_spawns level.ctf_spotspawn[local.alliedspot.abbr] "axis"
	waitthread block_spawns level.ctf_spotspawn[local.axisspot.abbr] "allies"

	level.ctf_settings["alliedbase"] = local.alliedspot.pos
	level.ctf_settings["axisbase"] = local.axisspot.pos
end


new_ctf_spot local.pos local.abbr:
	local.ent = spawn Listener
	local.ent.pos = local.pos
	local.ent.abbr = local.abbr
end local.ent


enumerate_spot_configs local.spots local.spotconfigs:
	local.result[0] = NIL
	local.k = 0
	for (local.i = 0; local.i < local.spots.size; local.i++)
	{
		local.spotname = local.spots[local.i].abbr
		local.spotconfig = local.spotconfigs[local.spotname]
		for (local.j = 1; local.j < (local.spotconfig.size + 1); local.j++)
		{
			local.result[local.k][0] = local.spots[local.i]
			local.result[local.k][1] = level.ctf_spotlu[local.spotconfig[local.j]]
			local.k++
		}
	}
end local.result


new_spawn local.abbr local.dmorigin local.alorigin local.axorigin local.angle:
	local.ent = spawn Listener
	local.ent.abbr = local.abbr
	local.ent.dmorigin = local.dmorigin
	local.ent.alorigin = local.alorigin
	local.ent.axorigin = local.axorigin
	local.ent.angles = ( 0 local.angle 0 )
end local.ent


block_spawns local.spawnnames local.team:
	for (local.i = 1; local.i < (local.spawnnames.size + 1); local.i++)
	{
		waitthread block_spawn level.ctf_spawnlu[local.spawnnames[local.i]] local.team
	}
end


block_spawn local.spawn local.team:
	local.ent = spawn script_model
	local.ent model "items/dm_50_healthbox.tik"

	if (local.team == "allies")
	{
		local.ent.origin = local.spawn.alorigin
	} else if (local.team == "axis")
	{
		local.ent.origin = local.spawn.axorigin
	} else
	{
		local.ent.origin = local.spawn.dmorigin
	}

	local.ent.angles = local.spawn.angles
	local.ent hide
	local.ent setsize ( 0 0 0 ) ( 1 1 16 )
end local.ent


setup_spawns:
	level.ctf_spawns[0]  = waitthread new_spawn "B"    ( 240 -104 -136 )  ( 240 -136 -136 )  ( 240 -72 -136 )  315 // Boiler Room
	level.ctf_spawns[1]  = waitthread new_spawn "ES1"  ( -408 -768 48 )   ( -408 -808 48 )   ( -400 -720 48 )    0 // East Bldg Staircase 1st Floor
	level.ctf_spawns[2]  = waitthread new_spawn "ES2"  ( -422 -809 288 )  ( -420 -845 288 )  ( -420 -773 288 )  45 // East Bldg Staircase 2nd Floor
	level.ctf_spawns[3]  = waitthread new_spawn "E2"   ( -632 -578 288 )  ( -632 -610 288 )  ( -664 -586 288 )  90 // East Bldg 2nd Floor
	level.ctf_spawns[4]  = waitthread new_spawn "W0"   ( -400 824 -136 )  ( -400 792 -136 )  ( -400 856 -136 )   0 // West Bldg Ground Floor
	level.ctf_spawns[5]  = waitthread new_spawn "WSB"  ( 418 839 -136 )   ( 418 807 -136 )   ( 418 871 -136 )  225 // West Bldg Staircase Bottom
	level.ctf_spawns[6]  = waitthread new_spawn "W1N"  ( 244 992 48 )     ( 244 984 48 )     ( 244 1008 48 )     0 // West Bldg 1st Floor North Side
	level.ctf_spawns[7]  = waitthread new_spawn "W2N"  ( 236 992 288 )    ( 236 984 288 )    ( 236 1008 288 )    0 // West Bldg 2nd Floor North Side
	level.ctf_spawns[8]  = waitthread new_spawn "W2SB" ( -432 919 288 )   ( -432 911 288 )   ( -432 935 288 )  270 // West Bldg 2nd Floor South Side Bathroom
	level.ctf_spawns[9]  = waitthread new_spawn "W1SB" ( -432 919 48 )    ( -432 911 48 )    ( -432 935 48 )   270 // West Bldg 1st Floor South Side Bathroom
	level.ctf_spawns[10] = waitthread new_spawn "W3SB" ( -448 991 528 )   ( -408 991 528 )   ( -488 991 528 )  270 // West Bldg 3rd Floor South Side Bathroom
	level.ctf_spawns[11] = waitthread new_spawn "W1S"  ( -312 928 48 )    ( -312 896 48 )    ( -312 960 48 )     0 // West Bldg 1st Floor South Side
	level.ctf_spawns[12] = waitthread new_spawn "W3S"  ( -312 936 528 )   ( -312 896 528 )   ( -312 984 528 )    0 // West Bldg 3rd Floor South Side
	level.ctf_spawns[13] = waitthread new_spawn "W2S"  ( -280 936 288 )   ( -280 904 288 )   ( -280 968 288 )    0 // West Bldg 2nd Floor South Side
	level.ctf_spawns[14] = waitthread new_spawn "W3N"  ( 248 992 528 )    ( 248 944 528 )    ( 248 1040 528 )    0 // West Bldg 3rd Floor North Side
	level.ctf_spawns[15] = waitthread new_spawn "SST"  ( -1152 -32 512 )  ( -1152 -72 512 )  ( -1152 8 512 )     0 // South Bldg Staircase Top
	level.ctf_spawns[16] = waitthread new_spawn "SSB"  ( -964 -184 262 )  ( -964 -192 262 )  ( -964 -168 262 ) 225 // South Bldg Staircase Bot
	level.ctf_spawns[17] = waitthread new_spawn "E1"   ( -484 -570 48 )   ( -484 -610 48 )   ( -484 -530 48 )    0 // East Bldg 1st Floor
	level.ctf_spawns[18] = waitthread new_spawn "S3"   ( -1136 816 512 )  ( -1136 784 512 )  ( -1136 848 512 )   0 // South Bldg 3rd Floor
	level.ctf_spawns[19] = waitthread new_spawn "ES0"  ( -336 -524 -147 ) ( -336 -556 -147 ) ( -336 -492 -147 )  0 // East Bldg Staircase Ground Floor
	level.ctf_spawns[20] = waitthread new_spawn "CS"   ( -664 808 -147 )  ( -704 800 -147 )  ( -624 816 -147 ) 270 // Courtyard South
	level.ctf_spawns[21] = waitthread new_spawn "CN"   ( 880 -48 -136 )   ( 912 -48 -136 )   ( 848 -48 -136 )   90 // Courtyard North
	level.ctf_spawns[22] = waitthread new_spawn "NR"   ( 1040 608 314 )   ( 1072 608 314 )   ( 1008 608 314 )  270 // North Roof
	level.ctf_spawns[23] = waitthread new_spawn "E1B"  ( -618 -614 48 )   ( -650 -614 48 )   ( -586 -614 48 )   90 // East Bldg 1st Floor Bathroom
	level.ctf_spawns[24] = waitthread new_spawn "S2"   ( -1136 817 260 )  ( -1136 785 260 )  ( -1136 849 260 )   0 // South Bldg 2nd Floor

	for (local.i = 0; local.i < level.ctf_spawns.size; local.i++)
	{
		level.ctf_spawnlu[level.ctf_spawns[local.i].abbr] = level.ctf_spawns[local.i]
	}
end
